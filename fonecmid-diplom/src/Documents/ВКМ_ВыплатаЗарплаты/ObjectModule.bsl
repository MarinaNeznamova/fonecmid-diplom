
Процедура ОбработкаПроведения(Отказ, Режим)
	// регистр ВКМ_ВзаиморасчетыССотрудниками Расход
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
	Для Каждого ТекСтрокаВыплаты Из Выплаты Цикл
		Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Сотрудник = ТекСтрокаВыплаты.Сотрудник;
		Движение.Сумма = ТекСтрокаВыплаты.Сумма;
	КонецЦикла;

	Движения.ВКМ_ВзаиморасчетыССотрудниками.БлокироватьДляИзменения = Истина;
	Движения.Записать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВКМ_ВыплатаЗарплатыВыплаты.Сотрудник КАК Сотрудник,
	|	ВКМ_ВыплатаЗарплатыВыплаты.Сумма КАК Сумма
	|ПОМЕСТИТЬ ВТ_Выплата
	|ИЗ
	|	Документ.ВКМ_ВыплатаЗарплаты.Выплаты КАК ВКМ_ВыплатаЗарплатыВыплаты
	|ГДЕ
	|	ВКМ_ВыплатаЗарплатыВыплаты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВКМ_ВзаиморасчетыССотрудникамиОстатки.СуммаОстаток КАК СуммаОстаток,
	|	ВТ_Выплата.Сотрудник КАК Сотрудник,
	|	ВТ_Выплата.Сумма КАК Сумма
	|ИЗ
	|	ВТ_Выплата КАК ВТ_Выплата
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВКМ_ВзаиморасчетыССотрудниками.Остатки(
	|				&ГраницаКонтроля,
	|				Сотрудник В
	|					(ВЫБРАТЬ
	|						ВТ_Выплата.Сотрудник КАК Сотрудник
	|					ИЗ
	|						ВТ_Выплата КАК ВТ_Выплата)) КАК ВКМ_ВзаиморасчетыССотрудникамиОстатки
	|		ПО ВТ_Выплата.Сотрудник = ВКМ_ВзаиморасчетыССотрудникамиОстатки.Сотрудник
	|ГДЕ
	|	ВКМ_ВзаиморасчетыССотрудникамиОстатки.СуммаОстаток < 0";
	
	Запрос.УстановитьПараметр("ГраницаКонтроля", Новый Граница(МоментВремени(), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл  
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("Проведение невозможно сотруднику %1 осталось выплатить %2. Перезаполните документ.", ВыборкаДетальныеЗаписи.Сотрудник, ВыборкаДетальныеЗаписи.Сумма+ ВыборкаДетальныеЗаписи.СуммаОстаток);
		Сообщение.Сообщить();			
		Отказ = Истина;
	КонецЦикла;
КонецПроцедуры

